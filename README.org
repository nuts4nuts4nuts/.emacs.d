#+title: Emacs configuration
#+PROPERTY: header-args:emacs-lisp :tangle ./init.el :mkdirp yes

* Org file that makes an el file

Hello! This is an org-mode file that automatically generates an init.el file for emacs configuration. Cool, huh?

** Auto-tangle Configuration Files

This snippet adds a hook to =org-mode= buffers so that =dkj/org-babel-tangle-config= gets executed each time such a buffer gets saved.  This function checks to see if the file being saved is in my .emacs.d directory and, if so, automatically exports the configuration here to the associated output files.

#+begin_src emacs-lisp
  (defun dkj/org-babel-tangle-config ()
    "Automatically tangle our config.org config file when we save it"
    (when (string-equal (file-name-directory (buffer-file-name))
			(expand-file-name user-emacs-directory))
      ;; Dynamic scoping to the rescue
      (let ((org-confirm-babel-evaluate nil))
	(org-babel-tangle))))

  (add-hook 'org-mode-hook (lambda () (add-hook 'after-save-hook #'dkj/org-babel-tangle-config)))
#+end_src

** Personal customization
*** Settings

These are some basic settings to make life a little better. They don't relate to packages or anything fancy like that.

#+begin_src emacs-lisp
  ;; Turn off all the bars
  (menu-bar-mode -1)
  (tool-bar-mode -1)
  (scroll-bar-mode -1)
  ;; Except the tab bar ;)
  (tab-bar-mode 1)

  ;; Always prompt before exiting
  (defalias 'yes-or-no-p 'y-or-n-p)
  (setq confirm-kill-emacs 'y-or-n-p)
  
  ;; Turn off the beeping with visible-bell
  (setq visible-bell t)

  ;; command is a lot more ergonomic than option
  ;; also, less confusing when going back and forth between mac and windows
  (setq mac-command-modifier 'meta)

  ;; Allow the mouse in terminal mode
  (xterm-mouse-mode 1)

  ;; I can't see a god damn at this small font size
  (defun dkj/font-height (height)
    "Prompts the user for a height and sets the font height.
     Uses the prefix arg if one is provided."
    (interactive "NHeight: ")
    (set-face-attribute 'default nil :height height))
  (dkj/font-height 140)

  ;; Automatically set view-mode when in a readonly buffer
  ;; Set a buffer as readonly with C-x C-q
  (setq view-read-only t)

  ;; Use bar cursor since it matches the emacs model better
  (setq-default cursor-type 'bar)

  ;; Add more context when scrolling around
  (setq next-screen-context-lines 10)

  ;; Move to the top or bottom of the buffer when scrolling
  (setq scroll-error-top-bottom 1)

  ;; Bigger global mark ring
  (setq global-mark-ring-max 50)

  ;; Show column 80
  (setq fill-column 80)
  (global-display-fill-column-indicator-mode)

  ;; Prefer vertical splits in more cases
  (setq split-width-threshold 90)
  (setq split-height-threshold 100)

  ;; Backup to the ~/.emacs.d/backups directory
  (setq backup-directory-alist
	`(("." . ,(concat user-emacs-directory "backups"))))

  ;; When I send mail from emacs, open the default mail client (because I haven't set up sending mail from emacs yet).
  (setq send-mail-function 'mailclient-send-it)

  ;; Put the menu bar in the tab bar, to help with discoverability
  (defun dkj/tab-bar-format-menu-bar ()
    "Produce the Menu button (denoted as λ) for the tab bar that shows the menu bar."
    `((menu-bar menu-item (propertize "λ" 'face 'tab-bar-tab-inactive)
		tab-bar-menu-bar :help "Menu Bar")))
  (add-to-list 'tab-bar-format #'dkj/tab-bar-format-menu-bar)

  ;; Winner mode remembers my window layouts
  (winner-mode 1)

  ;; Save minibuffer history across sessions
  (savehist-mode 1)

  ;; Automatically pull changes to files from disk
  (global-auto-revert-mode 1)

  ;; Default to lax whitespace in isearch and match any char
  (setq search-whitespace-regexp ".*")
  (setq isearch-lax-whitespace t)
  (setq isearch-regexp-lax-whitespace nil)

  ;; Override disabled commands
  (put 'narrow-to-region 'disabled nil)

  ;; fido settings
  ;; fido mode uses the built-in icomplete to emulate ido in every completing read interface
  ;; help:completing-read
  (fido-mode t)
  (define-key icomplete-fido-mode-map (kbd "C-.") nil)
  (define-key icomplete-fido-mode-map (kbd "C-,") nil)
  (define-key icomplete-minibuffer-map (kbd "C-.") nil)
  (define-key icomplete-minibuffer-map (kbd "C-,") nil)
  (setq completion-styles '(initials partial-completion flex)) ; > Emacs 27.1
  (setq completion-cycle-threshold 5)

#+end_src

*** Mappings

**** C-z as my personal keymap

By default, =C-c <letter>= is supposed to be reserved for user-defined commands.
This is not my favorite for a couple of reasons:
1. Some modes map =C-c <letter>= for some =<letter>= s
2. =C-c <letter>= is harder to press quickly than =C-c C-<letter>=, and =C-c C-<letter>= is explicitly reserved for modes to map.

So I want to make a own prefix, on which I can bind anything I like.
I've chosen =C-z= for this, since suspending Emacs is not something I intend on doing very often.

#+begin_src emacs-lisp
  (define-prefix-command 'dkj-keys)
  (global-set-key (kbd "C-z") #'dkj-keys)

  ;; Easily store links to things in org-mode format
  (define-key dkj-keys (kbd "C-l") #'org-store-link)
  ;; Capture something
  (define-key dkj-keys (kbd "C-c") #'org-capture)

  ;; Open the agenda
  (define-key dkj-keys (kbd "C-a") #'org-agenda)
  ;; Also map the main view to f12 since I use it all of the time
  (global-set-key (kbd "<f12>") (lambda () (interactive) (org-agenda nil "n")))
#+end_src

**** Bunch of mappings

#+begin_src emacs-lisp
  ;; Reserve this for tmux. Previously toggle-input-method
  (global-unset-key (kbd "C-\\"))

  ;; Easier window movement
  (global-set-key (kbd "C-x C-1") #'delete-other-windows)
  (global-set-key (kbd "C-x C-2") #'split-window-below)
  (global-set-key (kbd "C-x C-3") #'split-window-right)
  (global-set-key (kbd "C-x C-0") #'delete-window)
  (global-set-key (kbd "C-x C-o") #'other-window)

  ;; Kill line backwards
  (global-set-key (kbd "M-<backspace>") (lambda () (interactive) (kill-line 0)))

  ;; Use dwim versions of upcase and downcase instead of char/word/region-specific verions
  (global-set-key (kbd "M-u") #'upcase-dwim)
  (global-set-key (kbd "M-l") #'downcase-dwim)

  ;; Use cycle-spacing since it replaces just-one-space,
  ;; delete-horizontal-space, and delete-blank-lines all in one
  (global-set-key (kbd "M-SPC") #'cycle-spacing)

  ;; Bind M-v to go from the completions buffer to the minibuffer,
  ;; mirroring the minubuffer binding to go to completions
  (define-key completion-list-mode-map (kbd "M-v") #'switch-to-minibuffer)

  ;; Bind M-/ to hippie-expand instead of dabbrev-expand, since hippie does the same but more
  (global-set-key (kbd "M-/") #'hippie-expand)
#+end_src

*** Functions

#+begin_src emacs-lisp
  ;; C-c h to open this file, my config
  (defun dkj/open-config ()
    "Open this file"
    (interactive)
    (push-mark)
    (find-file "~/.emacs.d/README.org"))
  (define-key dkj-keys (kbd "C-h") #'dkj/open-config)

  ;; Pulse for a little longer than the default
  (setq pulse-delay 0.1)
  ;; Pulse the line when I get lost
  (defun dkj/pulse-line (&rest _)
    "Pulse the current line."
    (pulse-momentary-highlight-one-line (point)))
  (dolist (command '(scroll-up-command scroll-down-command
				       recenter-top-bottom other-window))
    (advice-add command :after #'dkj/pulse-line))
#+end_src
  
** Packages
*** Using packages

Packages are hip and cool and emacs is pretty good at using them.

#+begin_src emacs-lisp
  ;; Initialize package sources
  (require 'package)
  (setq package-archives '(("melpa" . "https://melpa.org/packages/")
			   ("org" . "https://orgmode.org/elpa/")
			   ("elpa" . "https://elpa.gnu.org/packages/")))
  (package-initialize)

  ;; Initialize use-package on non-Linux platforms
  (unless (package-installed-p 'use-package)
    (package-refresh-contents)
    (package-install 'use-package))
  
  (require 'use-package)
  (setq use-package-always-ensure t)
#+end_src

**** Automatic Package Updates

The auto-package-update package helps us keep our Emacs packages up to date!  It will prompt you after a certain number of days either at startup or at a specific time of day to remind you to update your packages.

You can also use =M-x auto-package-update-now= to update right now!

#+begin_src emacs-lisp
  (use-package auto-package-update
    :custom
    (auto-package-update-interval 7)
    (auto-package-update-prompt-before-update t)
    (auto-package-update-hide-results t)
    :config
    (auto-package-update-maybe)
    (auto-package-update-at-time "09:00"))
#+end_src

*** Which-key

[[https://github.com/justbur/emacs-which-key][which-key]] is a package which describes available key bindings interactively. If you use a binding which prefixes other bindings and then wait, it will pop up a small buffer with the available follow ups.

#+begin_src emacs-lisp
  (use-package which-key
    :config
    (which-key-mode))
#+end_src

*** Terminal fixes

**** term-keys

Terminal emacs is hell.
When we have to use it, [[https://github.com/CyberShadow/term-keys][term-keys]] helps.

#+begin_src emacs-lisp
  (when (not (display-graphic-p))
    (add-to-list 'package-archives
                 '("cselpa" . "https://elpa.thecybershadow.net/packages/"))
    (use-package term-keys
      :config
      (term-keys-mode t)))
#+end_src

**** Clipetty mode

[[https://github.com/spudlyo/clipetty][Clipetty]] is a minor mode that makes copying from terminal emacs and pasting in other applications work better.

#+begin_src emacs-lisp
  (when (not (display-graphic-p))
    (use-package clipetty
    :ensure t
    :hook (after-init . global-clipetty-mode)))
#+end_src

*** Pretty colors

#+begin_src emacs-lisp
  (use-package gruvbox-theme)

  (defun dkj/swap-themes ()
    (interactive)
    (progn
      (setq current-theme (car custom-enabled-themes))
      (mapc #'disable-theme custom-enabled-themes)
      (load-theme (cond
   ((eq current-theme 'gruvbox-dark-hard) 'gruvbox-light-hard)
   ((eq current-theme 'gruvbox-light-hard) 'gruvbox-dark-hard))
  t)))
  (define-key dkj-keys (kbd "C-\\") #'dkj/swap-themes)

  (load-theme 'gruvbox-dark-hard t)
#+end_src

*** Magit

[[https://github.com/magit/magit][Magit]] is a git frontend. People really like it. I'm used to fugitive. Anyway, this is Emacs, so we use Magit.

#+begin_src emacs-lisp
  (use-package magit)
#+end_src

*** Marginalia

[[https://github.com/minad/marginalia][Marginalia]] adds more context to minibuffer completions.

#+begin_src emacs-lisp
  (use-package marginalia
    :ensure t
    :config
    (marginalia-mode)
    :bind
    (:map minibuffer-local-map
	  ("M-A" . marginalia-cycle)))
#+end_src

*** Embark

[[https://github.com/oantolin/embark][Embark]] is a right-click menu for the keyboard.

#+begin_src emacs-lisp
  (use-package embark
  :ensure t
  :bind
  (("C-." . embark-act)         ;; pick some comfortable binding
   ("C-;" . embark-dwim)        ;; good alternative: M-.
   ("C-h B" . embark-bindings)) ;; alternative for `describe-bindings'
  :init
  ;; Optionally replace the key help with a completing-read interface
  (setq prefix-help-command #'embark-prefix-help-command)
  ;; Use the minimal indicator instead of the default mixed indicator
  (setq embark-indicators '(embark-minimal-indicator embark-highlight-indicator embark-isearch-highlight-indicator))
  :config
  ;; Hide the mode line of the Embark live/completions buffers
  (add-to-list 'display-buffer-alist
	       '("\\`\\*Embark Collect \\(Live\\|Completions\\)\\*"
		 nil
		 (window-parameters (mode-line-format . none)))))
#+end_src

*** Language support

Packages for configuring support of various computer languages

**** Markdown

In emacs we want to mostly write [[Org mode stuff][org-mode]] when it comes to markup languages. Still, markdown is useful.

#+begin_src emacs-lisp
  (use-package markdown-mode)
#+end_src

**** Racket

[[https://www.racket-mode.com/][racket-mode]] is so good dude. I can't go back to vim.

#+begin_src emacs-lisp
  (use-package racket-mode)
#+end_src

*** Org mode stuff

Org-mode is cool. That's an understatement, org-mode is one of the main reasons to use Emacs.

**** org-mode general settings

#+begin_src emacs-lisp
  (setq org-directory "~/org"
	org-default-notes-file "~/org/inbox.org"
					  ; ~/org and all subdirectories (assuming they don't have any .s in their names!) recursively
	org-id-locations-file "~/org/.org-id-locations"
	org-startup-truncated nil
	org-agenda-files (append '("~/org" "~/org/projects") (directory-files-recursively "~/org/projects/" "^[^.]+$" t)) 
	org-refile-targets '((nil :maxlevel . 9) (org-agenda-files :maxlevel . 9)))

  ;; Make inserting new list items a little cleaner
  (with-eval-after-load "org"
    (define-key org-mode-map (kbd "M-<return>") #'org-insert-item)
    (define-key org-mode-map (kbd "C-<return>") #'org-insert-heading))
#+end_src

**** Todo settings

#+begin_src emacs-lisp
  (setq org-todo-keywords
	'((sequence "TODO(t!)" "NEXT(n!)" "|" "DONE(d!)")
	  (sequence "|" "CANCELED(c!)")
	  (sequence "|" "HABIT(h!)"))
	org-clock-into-drawer t
	org-log-into-drawer t)
#+end_src

**** Capture templates

Quick cap

#+begin_src emacs-lisp
  (setq org-capture-templates
	(quote (("t" "Todo" entry (file "~/org/inbox.org")
		 "* TODO %?\n%U\n%a\n" :clock-in t :clock-resume t)
		("j" "Journal" entry (file+datetree "~/org/journal.org")
		 "* %? :JOURNAL:\n%U\n" :clock-in t :clock-resume t)
		("m" "Meeting" entry (file+datetree "~/org/journal.org")
		 "* %? :MEETING:\n%U\n" :clock-in t :clock-resume t)
		("i" "Interrupt" entry (file+datetree "~/org/journal.org")
		 "* %? :INTERRUPT:\n%U\n" :clock-in t :clock-resume t))))
#+end_src

**** Clock settings

Use org-mode to clock time spent on things.
Estimate time before starting tasks.
Get better at estimation through iteration.
Etc...
Largely taken from / inspired by http://doc.norang.ca/org-mode.html#Clocking

#+begin_src emacs-lisp
  ;; Show lot of clocking history so it's easy to pick items off the C-F11 list
  (setq org-clock-history-length 10)
  ;; Resume clocking task on clock-in if the clock is open
  (setq org-clock-in-resume t)
  ;; Sometimes I change tasks I'm clocking quickly - this removes clocked tasks with 0:00 duration
  (setq org-clock-out-remove-zero-time-clocks t)
  ;; Save the running clock and all clock history when exiting Emacs, load it on startup
  (setq org-clock-persist t)

  ;; Define things that show up as issues in clock check (v c in org-agenda)
  ;; Only thing I've changed is lowering the default max-gap from 5 minutes to 1
  ;; and lowering the default max-duration from 10 hours to 5 hours.
  (setq org-clock-consistency-checks '(:max-duration "5:00"
     :min-duration 0
     :max-gap "0:01"
     :gap-ok-around
     ("4:00")
     :default-face
     ((:background "DarkRed")
      (:foreground "white"))
     :overlap-face nil
     :gap-face nil
     :no-end-time-face nil
     :long-face nil
     :short-face nil))

  (defun dkj/global-clock-in ()
    (interactive)
    (org-clock-in '(4)))
  (define-key dkj-keys (kbd "C-i") #'dkj/global-clock-in)
#+end_src

**** Markdown export

Markdown is still useful, so we need a backend to export to it.

#+begin_src emacs-lisp
  (setq org-export-backends '(ascii html icalendar latex md odt))
#+end_src

**** Org execute other languages inline

With C-c C-c

#+begin_src emacs-lisp
  (org-babel-do-load-languages
   'org-babel-load-languages
   '(
     (python . t)
     ))

  (setq org-babel-python-command "python3")
#+end_src

*** GPTel

OpenAI GPT in emacs

#+begin_src emacs-lisp
  (use-package gptel
    :config
    (setq gptel-api-key (lambda () (getenv "OPENAIKEY"))))

  (define-key dkj-keys (kbd "C-<return>") #'gptel-send)
#+end_src

What does the above code do?

** Load other files
I like to keep everything in one file, but sometimes stuff needs to go in other files for cleanliness or confidentiality.

#+begin_src emacs-lisp
  ;; Load customize stuff
  (setq custom-file (concat user-emacs-directory "custom.el"))
  (when (file-exists-p custom-file)
    (load custom-file))

  ;; Load Google stuff
  (let ((googel (concat user-emacs-directory "google.el")))
    (when (file-exists-p googel)
      (load googel)))
#+end_src
